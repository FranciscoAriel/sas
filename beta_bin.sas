%LET n = 10000;
%LET m = 16;
%LET a = 2;
%LET b = 4;
%LET nsim = 500;
PROC IML;
    nsim = &nsim;
    START betabin (n,m,a,b) GLOBAL (nsim);
        x = J(n,1);
        DO i = 1 TO n;
            y = J(1,1);
            CALL RANDGEN(y,"BETA",a,b);
            xly = J(1,1);
            ylx = J(1,1);
            DO j = 1 TO nsim;
                CALL RANDGEN(xly,"BINOMIAL",y,m);
                CALL RANDGEN(ylx,"BETA",xly+a,m-xly+b);
                y = ylx;
            END;
            x[i] = xly;
        END;
        RETURN(x);
    FINISH;
    x = betabin(&n,&m,&a,&b);
    CREATE datos FROM x[COLNAME = "x"];
    APPEND FROM x;
QUIT;
PROC FREQ DATA = datos NOPRINT;
    TABLES x / OUT = tabla;
RUN;
DATA tabla;
    SET tabla;
	DROP percent;
	LABEL count = "Observada" prob = "Esperada";
	count = count/&n;
	/* Vea PDF function: r failures, given k successes r = 0,1,...*/
	prob = COMB(&m,x)*GAMMA(&a+&b)/(GAMMA(&a)*GAMMA(&b))*GAMMA(x+&a)*GAMMA(&m+&b-x)/GAMMA(&m+&a+&b);
RUN;
symbol1 value=dot;
symbol2 value=square color=red;
legend1 label=none
        position=(top center inside)
        mode=share;
TITLE "Simulación Beta-binomial con el muestreador de Gibbs";
PROC GPLOT DATA = tabla;
	PLOT count*x prob*x /OVERLAY LEGEND = legend1;
	TITLE2 "Comparación de probabilidades";
RUN;
QUIT;
PROC PRINT DATA = tabla LABEL;
	SUM count prob;
RUN;
