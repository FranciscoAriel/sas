PROC IML;
    n = 1000000;
    alfa = 11;
    beta = 11;
    x0 = (alfa - 1)/(alfa + beta -2);
	c = PDF("BETA",x0,alfa,beta);
    START arbeta(n,alfa,beta) GLOBAL (x0,c);
	x = J(n,1);
    IF (alfa = 1 & beta = 1) THEN DO;
        mensaje = "Distribución Uniforme. Algoritmo no funcionará";
        PRINT mensaje;
		CALL RANDGEN(x,"UNIFORM");
		CREATE datos FROM x[COLNAME = "x"];
    	APPEND FROM x;
        STOP;
    END;
    IF (alfa + beta < 2) THEN DO;
        mensaje = "No existe máximo global. Algoritmo puede fallar";
        PRINT mensaje;
        x1 = 0.0001;
        c1 = PDF("BETA",x1,alfa,beta);
        x2 = 0.9999;
        c2 = PDF("BETA",x2,alfa,beta);
        c = MAX(c1,c2);
        PRINT "Posible máximo óptimo",c;
    END;
    TITLE "Simulación por método de aceptación-rechazo";
    i = 0;
    j = 0;
    DO WHILE (i < n);
        y = J(1,1);
        CALL RANDGEN(y,"UNIFORM");
        ratio = PDF("BETA",y,alfa,beta)/c;
        u = J(1,1);
        CALL RANDGEN(u,"UNIFORM");
        IF (u < ratio) THEN DO;
            i = i + 1;
            x[i] = y;
        END;
        j = j + 1;
    END;
    q = {.15,.3,.5,.75,.95};
    pobs = J(5,1);
    pteo = J(5,1);
    DO k = 1 TO 5;
        pobs[k] = SUM(x<q[k])/n;
        pteo[k] = CDF("BETA",q[k],alfa,beta);
    END;
    tabla = q || pobs || pteo;
    IF (alfa + beta > 2) THEN DO;
	    inicial = x0 || c;
	    MATTRIB inicial COLNAME = {"Moda", "Constante óptima"} LABEL = "Valores iniciales";
		PRINT inicial;
    END;
    MATTRIB j LABEL = "Número de intentos";
    MATTRIB tabla COLNAME = {"Cuantil","Probabilidad estimada","Probabilidad teórica"} 
		LABEL = "Comparación de probabilidades observadas y esperadas";
	PRINT j,tabla;
    CREATE datos FROM x[COLNAME = "x"];
    APPEND FROM x;
    FINISH;
    CALL arbeta(n,alfa,beta);
QUIT;
PROC SGPLOT DATA = datos;
    HISTOGRAM x;
QUIT;
